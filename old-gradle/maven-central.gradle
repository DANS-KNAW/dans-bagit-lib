//This build file is for all the configuration specifics of building and deploying to maven-central
apply plugin: 'maven'
apply plugin: 'signing'

group = 'nl.knaw.dans'

artifacts {
  archives javadocJar
  archives sourcesJar
  archives jar
}

//artifacts need to be signed in order to upload them to maven central
signing {
//  required { !version.endsWith("SNAPSHOT") } //only sign an actual release
  sign configurations.archives
}

uploadArchives {
  repositories {
    mavenDeployer {
      if(!project.hasProperty("ossrhUsername")){ //so travis CI doesn't break...
        project.ext.ossrhUsername = "foo"
        project.ext.ossrhPassword = "foo"
      }
      beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

      repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
        authentication(userName: ossrhUsername, password: ossrhPassword)
      }

      pom.whenConfigured { pom ->
        pom.dependencies = pom.dependencies.findAll { dep -> dep.scope != 'test' } // removes the test scoped ones
      }
      pom.project {
        name 'bagit-java'
        packaging 'jar'
        // optionally artifactId can be defined here
        description 'The BAGIT LIBRARY is a software library intended to support the creation, manipulation, and validation of bags. Its current version is 0.97. It is version aware with the earliest supported version being 0.93.'
        url 'https://github.com/DANS-KNAW/bagit-java'

        scm {
          connection 'scm:git:https://github.com/DANS-KNAW/bagit-java'
          developerConnection 'scm:git:ssh://github.com/DANS-KNAW/bagit-java'
          url 'https://github.com/DANS-KNAW/bagit-java'
        }

        licenses {
          license {
            name 'No Copyright'
            url 'https://github.com/DANS-KNAW/bagit-java/blob/master/LICENSE.txt'
          }
        }

        developers {
          developer {
            id 'johnscancella'
            name 'John Scancella'
            email 'jsca@loc.gov'
          }
        }
      }
    }
  }
}

nexusStaging {
  packageGroup = "nl.knaw.dans"
  //stagingProfileId = "yourStagingProfileId" // when not defined will be got from server using "packageGroup"
}
